class Library {
    private final Map definitions = [:]

    def setDefinitions(Map values) {
        definitions.putAll(values)
    }

    def propertyMissing(String name) {
        def value = definitions.get(name)
        if (value == null) {
            throw new RuntimeException("Specified library '${name}' is not defined")
        }
        value
    }
}

// Attach to the root project to allow use from sub-projects
project.extensions.create("lib", Library)

// Versions for families of library (multiple artifacts with the same version)
// NOTE: Changing any of these versions may require update of other library versions

def springFrameworkVersion = '4.2.6.RELEASE'
def springBootVersion = '1.3.4.RELEASE'
def slf4jVersion = '1.7.21'
def junitJupiterVersion = '5.0.0-M2'
def junitPlatformRunnerVersion = '1.0.0-M2'


lib.definitions = [
        aspectjWeaver          : "org.aspectj:aspectjweaver:1.8.9",
        servletApi             : "javax.servlet:javax.servlet-api:3.1.0",

        springWeb              : "org.springframework:spring-web:${springFrameworkVersion}",
        springWebMvc           : "org.springframework:spring-webmvc:${springFrameworkVersion}",
        springBoot             : "org.springframework.boot:spring-boot:${springBootVersion}",
        springBootStarterAop   : "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}",
        springBootAutoConfig   : "org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}",
        springBootStarterTomcat: "org.springframework.boot:spring-boot-starter-tomcat:${springBootVersion}",
        slf4j                  : "org.slf4j:slf4j-api:${slf4jVersion}",
        prometheus             : 'io.prometheus:simpleclient:0.0.14',
        prometheusHotspot      : 'io.prometheus:simpleclient_hotspot:0.0.14',
        prometheusServlet      : 'io.prometheus:simpleclient_servlet:0.0.14',
        httpClient             : "org.apache.httpcomponents:httpclient:4.5.2",


        junitApi               : "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}",
        junitEngine            : "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}",
        junitPlatformRunner    : "org.junit.platform:junit-platform-runner:${junitPlatformRunnerVersion}",
        assertj                : "org.assertj:assertj-core:3.4.1",
        mokito                 : "org.mockito:mockito-core:2.0.106-beta"
]
